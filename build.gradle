buildscript {
    ext.kotlin_version = '1.0.4'
    ext.junit_version = '1.0.0-M2'
    repositories {
        mavenCentral()
        maven {
            url "https://plugins.gradle.org/m2/"
        }

    }
    dependencies {
        classpath("org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version")
        classpath("gradle.plugin.com.zoltu.gradle.plugin:git-versioning:2.0.28")
        classpath("org.junit.platform:junit-platform-gradle-plugin:$junit_version")
        classpath("com.jfrog.bintray.gradle:gradle-bintray-plugin:1.7")
    }
}

apply plugin: "com.zoltu.git-versioning"
apply plugin: "kotlin"
apply plugin: "maven"
apply plugin: 'org.junit.platform.gradle.plugin'
apply plugin: 'com.jfrog.bintray'

group = "jkly"
version = version()

junitPlatform {
    platformVersion "$junit_version"
    engines {
        include "spek"
    }
}


repositories {
    mavenCentral()
    maven { url "http://dl.bintray.com/markusamshove/maven" }
}

ext.gdxVersion = "1.9.4"
ext.spekVersion = "1.0.89"

dependencies {
    compile("org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version")
    compile("com.badlogicgames.gdx:gdx-box2d:$gdxVersion")

    testCompile("org.jetbrains.spek:spek-api:$spekVersion")
    testCompile("com.badlogicgames.gdx:gdx-box2d-platform:$gdxVersion:natives-desktop")
    testCompile("org.amshove.kluent:kluent:1.6")
    testCompile("com.google.truth:truth:0.28")
    testCompile("org.mockito:mockito-all:2.0.2-beta")
    testRuntime("org.jetbrains.spek:spek-junit-platform-engine:$spekVersion")
}

bintray {
    user = project.hasProperty("bintrayUser") ? project.property("bintrayUser") : System.getenv("BINTRAY_USER")
    key = project.hasProperty("bintrayApiKey") ? project.property("bintrayApiKey") : System.getenv("BINTRAY_API_KEY")
    configurations = ["archives"]
    publish = true

    pkg {
        repo = "releases"
        name = "gdx-box2d-kotlin"
        licenses = ["Apache-2.0"]
        vcsUrl = "https://github.com/Jkly/gdx-box2d-kotlin"
    }
}

def version() {
    def versionInfo = ZoltuGitVersioning.versionInfo
    return "${versionInfo.major}.${versionInfo.minor}.${versionInfo.commitCount}-${versionInfo.sha}"
}